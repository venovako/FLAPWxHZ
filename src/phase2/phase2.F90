PROGRAM PHASE2
  USE BINIO1
  USE BLAS_UTILS
  USE JQR
  USE TIMER
  IMPLICIT NONE

  INTEGER, PARAMETER :: FNL = 20

  DOUBLE COMPLEX, ALLOCATABLE :: Y(:,:), W(:,:), WW(:,:)
  DOUBLE PRECISION, ALLOCATABLE :: FCT(:)
  INTEGER, ALLOCATABLE :: JJ(:), J(:), P(:), ROW(:)

  CHARACTER(LEN=FNL,KIND=c_char) :: FN
  INTEGER :: M, N, LDY, LDW, INFO, T

  EXTERNAL :: ZLASET

  CALL READCL(FN, M, N, INFO)
  IF (INFO .NE. 0) THEN
     IF (INFO .LT. 0) THEN
        WRITE (ULOG,'(A,I2)') 'Cannot read argument', -INFO
     ELSE
        WRITE (ULOG,'(A,I2)') 'Illegal value of argument', INFO
     END IF
     STOP 'phase2.exe FN M N'
  END IF

  LDY = LDALIGN(M, ZALIGN)
  LDW = LDALIGN(M, ZALIGN)

  ALLOCATE(Y(LDY,N))
  ALLOCATE(W(LDW,N))
  ALLOCATE(JJ(M))
  ALLOCATE(J(N))
  ALLOCATE(P(N))
  ALLOCATE(FCT(N))
  ALLOCATE(ROW(N))
  ALLOCATE(WW(LDW,N))

  CALL BIO_READ_ALL(FN, M, N, Y, LDY, WW, LDW, JJ, INFO)
  IF (INFO .NE. 0) THEN
     WRITE (ULOG,'(I20)') INFO
     STOP 'BIO_READ_ALL'
  END IF

  INFO = BLAS_PREPARE()

  ! JQR(YY)
  T = GET_THREAD_NS()
  CALL ZJQR(M, N, Y, LDY, JJ, W, LDW, J, P, FCT, ROW, INFO)
  T = GET_THREAD_NS() - T
  WRITE (UOUT,'(F11.6,A,I20,A)',ADVANCE='NO') (T * DNS2S), ',', INFO, ','
  IF (INFO .LT. 0) STOP 'ZJQR'

  ! PIV(WW)
  T = GET_THREAD_NS()
  CALL ZCPIVCP(M, N, WW, LDW, W, LDW, P, INFO)
  DEALLOCATE(WW)
  T = GET_THREAD_NS() - T
  WRITE (UOUT,'(F11.6,A)',ADVANCE='NO') (T * DNS2S), ','
  IF (INFO .NE. 0) THEN
     WRITE (ULOG,'(I20)') INFO
     STOP 'ZCPIVCP'
  END IF
  
  ! QR(WW)
  T = GET_THREAD_NS()
  CALL ZTSR(M, N, W, LDW, INFO)
  T = GET_THREAD_NS() - T
  WRITE (UOUT,'(F11.6)') (T * DNS2S)
  IF (INFO .NE. 0) THEN
     WRITE (ULOG,'(I20)') INFO
     STOP 'ZTSR'
  END IF

  CALL BIO_WRITE_ALL(FN, N, Y, LDY, W, LDW, J, P, INFO)
  IF (INFO .NE. 0) THEN
     WRITE (ULOG,'(I20)') INFO
     STOP 'BIO_WRITE_ALL'
  END IF

  DEALLOCATE(ROW)
  DEALLOCATE(FCT)
  DEALLOCATE(P)
  DEALLOCATE(J)
  DEALLOCATE(JJ)
  DEALLOCATE(W)
  DEALLOCATE(Y)

CONTAINS
#include "readcl.F90"
#include "bio.F90"
END PROGRAM PHASE2

PROGRAM PHASE2
  USE BINIO
  USE JQR
  USE TIMER
  USE OMP_LIB
  IMPLICIT NONE

  COMPLEX(KIND=DWP), ALLOCATABLE, TARGET :: Y(:,:), W(:,:), WW(:,:)
  !DIR$ ATTRIBUTES ALIGN:ALIGNB :: Y,W,WW
  REAL(KIND=DWP), ALLOCATABLE :: FCT(:), WORK(:)
  !DIR$ ATTRIBUTES ALIGN:ALIGNB :: FCT,WORK
  INTEGER, ALLOCATABLE, TARGET :: JJ(:), P(:), ROW(:)
  !DIR$ ATTRIBUTES ALIGN:ALIGNB :: JJ,P,ROW

  CHARACTER(LEN=FNL,KIND=c_char) :: FN
  INTEGER :: M, N, TPC, LDY, LDW, INFO, T

  EXTERNAL :: ZLASET

  IF (.NOT. VERIFY_MIN_MAX()) STOP 'MIN and/or MAX do NOT handle NaNs properly!'
  CALL READCL(FN, M, N, TPC, INFO)
  IF (INFO .NE. 0) THEN
     IF (INFO .LT. 0) THEN
        WRITE (ULOG,'(A,I2)') 'Cannot read argument', -INFO
     ELSE
        WRITE (ULOG,'(A,I2)') 'Illegal value of argument', INFO
     END IF
     STOP 'phase2.exe FN M N TPC'
  END IF

  LDY = LDALIGN(M, ZALIGN)
  LDW = LDALIGN(M, ZALIGN)

  ALLOCATE(Y(LDY,N))
  ALLOCATE(W(LDW,N))
  ALLOCATE(JJ(M))
  ALLOCATE(P(N))
  ALLOCATE(ROW(N))
  ALLOCATE(WW(LDW,N))
  ALLOCATE(FCT(N))
  ALLOCATE(WORK(N))

  CALL BIO_READ_ALL(FN, M, N, Y, LDY, WW, LDW, JJ, INFO)
  IF (INFO .NE. 0) THEN
     WRITE (ULOG,'(I2)') INFO
     STOP 'BIO_READ_ALL'
  END IF

  INFO = BLAS_PREPARE()

  ! JQR(YY)
  T = GET_THREAD_NS()
  CALL ZJR(M, N, Y, LDY, JJ, W, LDW, TPC, P, FCT, ROW, WORK, INFO)
  DEALLOCATE(WORK)
  DEALLOCATE(FCT)
  T = GET_THREAD_NS() - T
  WRITE (UOUT,'(F11.6,A,I20,A)',ADVANCE='NO') (T * DNS2S), ',', INFO, ','
  IF (INFO .LT. 0) STOP 'ZJR'

  ! PIV(WW)
  T = GET_THREAD_NS()
  CALL ZCPIVCP(M, N, WW, LDW, W, LDW, P, INFO)
  DEALLOCATE(WW)
  T = GET_THREAD_NS() - T
  WRITE (UOUT,'(F11.6,A)',ADVANCE='NO') (T * DNS2S), ','
  IF (INFO .NE. 0) THEN
     WRITE (ULOG,'(I2)') INFO
     STOP 'ZCPIVCP'
  END IF
  
  ! QR(WW)
  T = GET_THREAD_NS()
#ifdef USE_ZGEQRF
  CALL ZQRF(M, N, W, LDW, INFO)
#else
  CALL ZTSR(M, N, W, LDW, INFO)
#endif
  T = GET_THREAD_NS() - T
  WRITE (UOUT,'(F11.6)') (T * DNS2S)
  IF (INFO .NE. 0) THEN
     WRITE (ULOG,'(I2)') INFO
#ifdef USE_ZGEQRF
     STOP 'ZQRF'
#else
     STOP 'ZTSR'
#endif
  END IF

  CALL BIO_WRITE_ALL(FN, N, Y, LDY, W, LDW, JJ, P, ROW, INFO)
  IF (INFO .NE. 0) THEN
     WRITE (ULOG,'(I2)') INFO
     STOP 'BIO_WRITE_ALL'
  END IF

  DEALLOCATE(ROW)
  DEALLOCATE(P)
  DEALLOCATE(JJ)
  DEALLOCATE(W)
  DEALLOCATE(Y)

CONTAINS
#include "readcl.F90"
#include "bio.F90"
END PROGRAM PHASE2

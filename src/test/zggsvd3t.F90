PROGRAM ZGGSVD3T
  USE BINIO
  USE BLAS_UTILS
  USE TIMER
  IMPLICIT NONE

  CHARACTER, PARAMETER :: JOBU='U',JOBV='V',JOBQ='Q', TRANSA='N',TRANSB='C'

  CHARACTER(LEN=252,KIND=c_char) :: FN

  INTEGER :: M,N,P,K,L, LDA,LDB,LDU,LDV,LDQ,LDX, LWORK,LRWORK,LIWORK, INFO, SZ,FD
  DOUBLE COMPLEX :: WORK1(1)

  DOUBLE COMPLEX, ALLOCATABLE, TARGET :: A(:,:),B(:,:)
  !DIR$ ATTRIBUTES ALIGN:ALIGNB :: A,B
  DOUBLE COMPLEX, ALLOCATABLE :: U(:,:),V(:,:),Q(:,:),X(:,:), WORK(:)
  !DIR$ ATTRIBUTES ALIGN:ALIGNB :: U,V,Q,X
  DOUBLE PRECISION, ALLOCATABLE :: ALPHA(:),BETA(:), RWORK(:)
  INTEGER, ALLOCATABLE :: IWORK(:)
#ifdef USE_X200
  !DIR$ ATTRIBUTES MEMKIND:HBW, ALIGN:ALIGNB :: ALPHA,BETA, WORK,RWORK,IWORK
#else
  !DIR$ ATTRIBUTES ALIGN:ALIGNB :: ALPHA,BETA, WORK,RWORK,IWORK
#endif

  EXTERNAL :: ZGGSVD3, ZGEMM

  CALL READCL(M, N, FN, INFO)
  IF (INFO .NE. 0) THEN
     WRITE (ULOG,'(I2,A)',ADVANCE='NO') INFO, ' '
     STOP 'READCL'
  END IF

  P = M
  INFO = BLAS_PREPARE()

  CALL BOPEN_RO((TRIM(FN)//c_char_'.Y'), SZ, FD)
  IF (FD .LT. 0) STOP 'BOPEN_Y_RO'
  LDA = M; ALLOCATE(A(LDA,N)); A = Z_ZERO

  ! read F into A
  CALL BREAD_YW(FD, A, M, N, SZ, INFO)
  IF (INFO .NE. 0) STOP 'BREAD_Y'
  CALL BCLOSE(FD)

  CALL BOPEN_RO((TRIM(FN)//c_char_'.W'), SZ, FD)
  IF (FD .LT. 0) STOP 'BOPEN_W_RO'
  LDB = P; ALLOCATE(B(LDB,N)); B = Z_ZERO

  ! read G into B
  CALL BREAD_YW(FD, B, M, N, SZ, INFO)
  IF (INFO .NE. SZ) STOP 'BREAD_W'
  CALL BCLOSE(FD)

  LDU = M; ALLOCATE(U(LDU,M)); U = Z_ZERO
  LDV = P; ALLOCATE(V(LDV,P)); V = Z_ZERO
  LDQ = N; ALLOCATE(Q(LDQ,N)); Q = Z_ZERO
  LDX = N; ALLOCATE(X(LDX,N)); X = Z_ZERO

  ALLOCATE(ALPHA(N)); ALPHA = D_ZERO
  ALLOCATE(BETA(N)); BETA = D_ZERO

  LRWORK = 2 * N; ALLOCATE(RWORK(LRWORK)); RWORK = D_ZERO
  LIWORK = N; ALLOCATE(IWORK(LIWORK)); IWORK = D_ZERO

  LWORK = -1; WORK1 = Z_ZERO

  K = 0; L = 0; INFO = 0
  CALL ZGGSVD3(JOBU,JOBV,JOBQ, M,N,P,K,L, A,LDA, B,LDB, ALPHA,BETA, U,LDU, V,LDV, Q,LDQ, WORK1,LWORK, RWORK,IWORK, INFO)
  IF (INFO .NE. 0) THEN
     WRITE (ULOG,'(I3,A)',ADVANCE='NO') INFO, ' '
     STOP 'ZGGSVD3 workspace query'
  END IF
  LWORK = INT(DBLE(WORK1(1))); ALLOCATE(WORK(LWORK)); WORK = Z_ZERO

  SZ = GET_THREAD_NS()
  CALL ZGGSVD3(JOBU,JOBV,JOBQ, M,N,P,K,L, A,LDA, B,LDB, ALPHA,BETA, U,LDU, V,LDV, Q,LDQ, WORK,LWORK, RWORK,IWORK, INFO)
  IF (INFO .NE. 0) THEN
     WRITE (ULOG,'(I3,A)',ADVANCE='NO') INFO, ' '
     STOP 'ZGGSVD3'
  END IF
  IF ((K + L) .NE. N) THEN
     WRITE (ULOG,'(4(I6,A))',ADVANCE='NO') K,'+', L,'=', (K+L),'<', N,' '
     STOP 'ZGGSVD3 numerical rank'
  END IF
  ! X = A(1:K+L, N-K-L+1:N) * Q^H = A(1:N, 1:N) * Q^H
  CALL ZGEMM(TRANSA,TRANSB, N,N,N, Z_ONE, A,LDA, Q,LDQ, Z_ZERO, X,LDX)
  SZ = GET_THREAD_NS() - SZ
  WRITE (UOUT,'(F15.6)') (SZ * DNS2S)

  IF (ALLOCATED(WORK)) DEALLOCATE(WORK)
  IF (ALLOCATED(IWORK)) DEALLOCATE(IWORK)
  IF (ALLOCATED(RWORK)) DEALLOCATE(RWORK)

  IF (ALLOCATED(BETA)) DEALLOCATE(BETA)
  IF (ALLOCATED(ALPHA)) DEALLOCATE(ALPHA)

  IF (ALLOCATED(X)) DEALLOCATE(X)
  IF (ALLOCATED(Q)) DEALLOCATE(Q)
  IF (ALLOCATED(V)) DEALLOCATE(V)
  IF (ALLOCATED(U)) DEALLOCATE(U)

  IF (ALLOCATED(B)) DEALLOCATE(B)
  IF (ALLOCATED(A)) DEALLOCATE(A)

CONTAINS

  SUBROUTINE READCL(M, N, FN, INFO)
    IMPLICIT NONE
    INTEGER, INTENT(OUT) :: M, N, INFO
    CHARACTER(LEN=*,KIND=c_char), INTENT(OUT) :: FN

    CHARACTER(LEN=24) :: ARG
    INTEGER :: TMP

    INFO = 0
    IF (COMMAND_ARGUMENT_COUNT() .NE. 3) STOP 'zggsvd3t.exe M N FN'
    ARG = '                        '
    TMP = 0

    CALL GET_COMMAND_ARGUMENT(1, ARG, TMP, INFO)
    IF (INFO .NE. 0) THEN
       INFO = -1
       RETURN
    END IF
    READ (ARG,*) M
    IF (M .LE. 0) THEN
       INFO = 1
       RETURN
    END IF

    CALL GET_COMMAND_ARGUMENT(2, ARG, TMP, INFO)
    IF (INFO .NE. 0) THEN
       INFO = -2
       RETURN
    END IF
    READ (ARG,*) N
    IF ((N .LE. 0) .OR. (M .LT. N)) THEN
       INFO = 2
       RETURN
    END IF

    CALL GET_COMMAND_ARGUMENT(3, ARG, TMP, INFO)
    IF (INFO .NE. 0) THEN
       INFO = -3
       RETURN
    END IF
    FN = TRIM(ARG)
    IF (LEN_TRIM(FN) .LE. 0) THEN
       INFO = 3
       RETURN
    END IF
  END SUBROUTINE READCL

  SUBROUTINE BREAD_YW(FD, YW, M, N, SZ, INFO)
    IMPLICIT NONE
    INTEGER, INTENT(IN) :: FD, M, N
    DOUBLE COMPLEX, INTENT(OUT), TARGET :: YW(M,N)
    INTEGER, INTENT(OUT) :: SZ, INFO

    INFO = 0

    SZ = BREAD(FD, C_LOC(YW), C_SIZEOF(YW), 0)
    IF (SZ .NE. C_SIZEOF(YW)) INFO = 1
  END SUBROUTINE BREAD_YW
END PROGRAM ZGGSVD3T

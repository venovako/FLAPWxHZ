PROGRAM ZGENT
  USE BINIO
  USE BLAS_UTILS
  IMPLICIT NONE

  CHARACTER, PARAMETER :: DIST = 'U', PACK = 'N'
  INTEGER, PARAMETER :: SEED(4) = (/ 11, 13, 17, 19 /), MODE = 6
  DOUBLE PRECISION, PARAMETER :: COND = D_ONE, DMAX = D_ONE
  
  CHARACTER(LEN=252,KIND=c_char) :: FN
  CHARACTER :: SYM
  INTEGER :: M, N, KL, KU, LDA, INFO, ISEED(4), FD, SZ

  DOUBLE COMPLEX, ALLOCATABLE, TARGET :: A(:,:)
  !DIR$ ATTRIBUTES ALIGN:ALIGNB :: A
  DOUBLE PRECISION, ALLOCATABLE :: D(:)
  INTEGER, ALLOCATABLE, TARGET :: J(:)
  DOUBLE COMPLEX, ALLOCATABLE :: WORK(:)
#ifdef USE_X200
  !DIR$ ATTRIBUTES MEMKIND:HBW, ALIGN:ALIGNB :: D,J,WORK
#else
  !DIR$ ATTRIBUTES ALIGN:ALIGNB :: D,J,WORK
#endif

  EXTERNAL :: ZLATMS

  CALL READCL(M, N, FN, INFO)
  IF (INFO .NE. 0) THEN
     WRITE (ULOG,'(I2,A)',ADVANCE='NO') INFO, ' '
     STOP 'READCL'
  END IF

  INFO = BLAS_PREPARE()

  LDA = M; ALLOCATE(A(LDA,N)); A = Z_ZERO
  ALLOCATE(D(N)); D = D_ZERO
  ALLOCATE(WORK(3*M)); WORK = Z_ZERO

  ISEED = SEED
  IF (M .EQ. N) THEN
     SYM = 'P'
  ELSE
     SYM = 'N'
  END IF
  KL = M - 1
  KU = N - 1

  ! generate F
  CALL ZLATMS(M, N, DIST, ISEED, SYM, D, MODE, COND, DMAX, KL, KU, PACK, A, LDA, WORK, INFO)
  IF (INFO .NE. 0) THEN
     WRITE (ULOG,'(I5,A)',ADVANCE='NO') INFO, ' '
     STOP 'ZLATMS 1'
  END IF

  FD = -1
  SZ = M * N * C_SIZEOF(Z_ZERO)
  CALL BOPEN_Y_RW(FN, SZ, FD)
  IF (FD .LT. 0) STOP 'BOPEN_Y_RW'
  CALL BWRITE_Y(FD, A, M/2, N, 0, INFO)
  IF (INFO .NE. SZ) STOP 'BWRITE_Y'
  CALL BCLOSE(FD)

  ! generate G
  CALL ZLATMS(M, N, DIST, ISEED, SYM, D, MODE, COND, DMAX, KL, KU, PACK, A, LDA, WORK, INFO)
  IF (INFO .NE. 0) THEN
     WRITE (ULOG,'(I5,A)',ADVANCE='NO') INFO, ' '
     STOP 'ZLATMS 2'
  END IF

  CALL BOPEN_W_RW(FN, SZ, FD)
  IF (FD .LT. 0) STOP 'BOPEN_W_RW'
  CALL BWRITE_W(FD, A, M/2, N, 0, INFO)
  IF (INFO .NE. SZ) STOP 'BWRITE_W'
  CALL BCLOSE(FD)

  IF (ALLOCATED(WORK)) DEALLOCATE(WORK)
  IF (ALLOCATED(D)) DEALLOCATE(D)
  IF (ALLOCATED(A)) DEALLOCATE(A)

  ALLOCATE(J(M))
  J = 1

  SZ = M * C_SIZEOF(0)
  CALL BOPEN_J_RW(FN, SZ, FD)
  IF (FD .LT. 0) STOP 'BOPEN_J_RW'
  CALL BWRITE_J(FD, J, M/2, 0, INFO)
  IF (INFO .NE. SZ) STOP 'BWRITE_J'
  CALL BCLOSE(FD)

  IF (ALLOCATED(J)) DEALLOCATE(J)

CONTAINS

  SUBROUTINE READCL(M, N, FN, INFO)
    IMPLICIT NONE
    INTEGER, INTENT(OUT) :: M, N, INFO
    CHARACTER(LEN=*,KIND=c_char), INTENT(OUT) :: FN

    CHARACTER(LEN=24) :: ARG
    INTEGER :: TMP

    INFO = 0
    IF (COMMAND_ARGUMENT_COUNT() .NE. 3) STOP 'zgent.exe M N FN'
    ARG = '                        '
    TMP = 0

    CALL GET_COMMAND_ARGUMENT(1, ARG, TMP, INFO)
    IF (INFO .NE. 0) THEN
       INFO = -1
       RETURN
    END IF
    READ (ARG,*) M
    IF ((M .LE. 0) .OR. (MOD(M,2) .NE. 0)) THEN
       INFO = 1
       RETURN
    END IF

    CALL GET_COMMAND_ARGUMENT(2, ARG, TMP, INFO)
    IF (INFO .NE. 0) THEN
       INFO = -2
       RETURN
    END IF
    READ (ARG,*) N
    IF ((N .LE. 0) .OR. (M .LT. N)) THEN
       INFO = 2
       RETURN
    END IF

    CALL GET_COMMAND_ARGUMENT(3, ARG, TMP, INFO)
    IF (INFO .NE. 0) THEN
       INFO = -3
       RETURN
    END IF
    FN = TRIM(ARG)
    IF (LEN_TRIM(FN) .LE. 0) THEN
       INFO = 3
       RETURN
    END IF
  END SUBROUTINE READCL
END PROGRAM ZGENT

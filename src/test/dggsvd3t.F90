PROGRAM DGGSVD3T
  USE BINIO
  USE BLAS_UTILS
  USE TIMER
  USE OMP_LIB
  IMPLICIT NONE

  ! CHARACTER, PARAMETER :: JOBU='U',JOBV='V',JOBQ='Q'
  CHARACTER, PARAMETER :: JOBU='N',JOBV='N',JOBQ='N'

  CHARACTER(LEN=FNL,KIND=c_char) :: FN

  INTEGER :: M,N,P,K,L, LDA,LDB,LDU,LDV,LDQ, LWORK,LIWORK, INFO, SZ,FD
  REAL(KIND=DWP) :: WORK1(1)

  REAL(KIND=DWP), ALLOCATABLE, TARGET :: A(:,:),B(:,:)
  !DIR$ ATTRIBUTES ALIGN:ALIGNB :: A,B
  REAL(KIND=DWP), ALLOCATABLE :: U(:,:),V(:,:),Q(:,:)
  !DIR$ ATTRIBUTES ALIGN:ALIGNB :: U,V,Q
  REAL(KIND=DWP), ALLOCATABLE :: ALPHA(:),BETA(:)
  REAL(KIND=DWP), ALLOCATABLE, TARGET :: WORK(:)
  INTEGER, ALLOCATABLE :: IWORK(:)
#ifdef USE_X200
  !DIR$ ATTRIBUTES MEMKIND:HBW, ALIGN:ALIGNB :: ALPHA,BETA, WORK,IWORK
#else
  !DIR$ ATTRIBUTES ALIGN:ALIGNB :: ALPHA,BETA, WORK,IWORK
#endif

  EXTERNAL :: DGGSVD3, DLASRT

  CALL READCL(M, N, FN, INFO)
  IF (INFO .NE. 0) THEN
     WRITE (ULOG,'(I2,A)',ADVANCE='NO') INFO, ' '
     ERROR STOP 'READCL'
  END IF

  P = M
  INFO = BLAS_PREPARE()

  CALL BOPEN_RO((TRIM(FN)//c_char_'.Y'), SZ, FD)
  IF (FD .LT. 0) ERROR STOP 'BOPEN_Y_RO'
  LDA = M; ALLOCATE(A(LDA,N)); A = D_ZERO

  ! read F into A
  CALL BREAD_YW(FD, A, M, N, SZ, INFO)
  IF (INFO .NE. 0) ERROR STOP 'BREAD_Y'
  CALL BCLOSE(FD)

  CALL BOPEN_RO((TRIM(FN)//c_char_'.W'), SZ, FD)
  IF (FD .LT. 0) ERROR STOP 'BOPEN_W_RO'
  LDB = P; ALLOCATE(B(LDB,N)); B = D_ZERO

  ! read G into B
  CALL BREAD_YW(FD, B, M, N, SZ, INFO)
  IF (INFO .NE. 0) ERROR STOP 'BREAD_W'
  CALL BCLOSE(FD)

  LDU = M; ALLOCATE(U(LDU,M)); U = D_ZERO
  LDV = P; ALLOCATE(V(LDV,P)); V = D_ZERO
  LDQ = N; ALLOCATE(Q(LDQ,N)); Q = D_ZERO

  ALLOCATE(ALPHA(N)); ALPHA = D_ZERO
  ALLOCATE(BETA(N)); BETA = D_ZERO

  LIWORK = N; ALLOCATE(IWORK(LIWORK)); IWORK = D_ZERO

  LWORK = -1; WORK1 = D_ZERO

  K = 0; L = 0; INFO = 0
  CALL DGGSVD3(JOBU,JOBV,JOBQ, M,N,P,K,L, A,LDA, B,LDB, ALPHA,BETA, U,LDU, V,LDV, Q,LDQ, WORK1,LWORK, IWORK, INFO)
  IF (INFO .NE. 0) THEN
     WRITE (ULOG,'(I3,A)',ADVANCE='NO') INFO, ' '
     ERROR STOP 'DGGSVD3 workspace query'
  END IF
  LWORK = MAX(N, CEILING(WORK1(1))); ALLOCATE(WORK(LWORK)); WORK = D_ZERO

  SZ = GET_SYS_US()
  CALL DGGSVD3(JOBU,JOBV,JOBQ, M,N,P,K,L, A,LDA, B,LDB, ALPHA,BETA, U,LDU, V,LDV, Q,LDQ, WORK,LWORK, IWORK, INFO)
  IF (INFO .NE. 0) THEN
     WRITE (ULOG,'(I3,A)',ADVANCE='NO') INFO, ' '
     ERROR STOP 'DGGSVD3'
  END IF
  IF ((K + L) .NE. N) THEN
     WRITE (ULOG,'(4(I6,A))',ADVANCE='NO') K,'+', L,'=', (K+L),'<', N,' '
     ERROR STOP 'DGGSVD3 numerical rank'
  END IF
  SZ = GET_SYS_US() - SZ
  WRITE (UOUT,'(F15.6)') (SZ * DUS2S)

  !DIR$ VECTOR ALWAYS ASSERT, ALIGNED
  DO INFO = 1, N
     WORK(INFO) = ALPHA(INFO) / BETA(INFO)
  END DO
  CALL DLASRT('D', N, WORK, INFO)
  IF (INFO .NE. 0) ERROR STOP 'DLASRT'

  SZ = N * C_SIZEOF(D_ZERO); FD = -1
  CALL BOPEN_RW((TRIM(FN)//c_char_'.SS'), SZ, FD)
  IF (FD .LT. 0) ERROR STOP 'BOPEN_SS_RW'
  CALL BWRITE_SS(FD, WORK, N, INFO)
  IF (INFO .NE. SZ) ERROR STOP 'BWRITE_SS'
  CALL BCLOSE(FD)

  IF (ALLOCATED(WORK)) DEALLOCATE(WORK)
  IF (ALLOCATED(IWORK)) DEALLOCATE(IWORK)

  IF (ALLOCATED(BETA)) DEALLOCATE(BETA)
  IF (ALLOCATED(ALPHA)) DEALLOCATE(ALPHA)

  IF (ALLOCATED(Q)) DEALLOCATE(Q)
  IF (ALLOCATED(V)) DEALLOCATE(V)
  IF (ALLOCATED(U)) DEALLOCATE(U)

  IF (ALLOCATED(B)) DEALLOCATE(B)
  IF (ALLOCATED(A)) DEALLOCATE(A)

CONTAINS

  SUBROUTINE READCL(M, N, FN, INFO)
    IMPLICIT NONE
    INTEGER, INTENT(OUT) :: M, N, INFO
    CHARACTER(LEN=*,KIND=c_char), INTENT(OUT) :: FN

    CHARACTER(LEN=FNL) :: ARG
    INTEGER :: TMP

    INFO = 0
    IF (COMMAND_ARGUMENT_COUNT() .NE. 3) ERROR STOP 'dggsvd3t.exe M N FN'
    ARG = '                        '
    TMP = 0

    CALL GET_COMMAND_ARGUMENT(1, ARG, TMP, INFO)
    IF (INFO .NE. 0) THEN
       INFO = -1
       RETURN
    END IF
    READ (ARG,*) M
    IF (M .LE. 0) THEN
       INFO = 1
       RETURN
    END IF

    CALL GET_COMMAND_ARGUMENT(2, ARG, TMP, INFO)
    IF (INFO .NE. 0) THEN
       INFO = -2
       RETURN
    END IF
    READ (ARG,*) N
    IF ((N .LE. 0) .OR. (M .LT. N)) THEN
       INFO = 2
       RETURN
    END IF

    CALL GET_COMMAND_ARGUMENT(3, ARG, TMP, INFO)
    IF (INFO .NE. 0) THEN
       INFO = -3
       RETURN
    END IF
    FN = TRIM(ARG)
    IF (LEN_TRIM(FN) .LE. 0) THEN
       INFO = 3
       RETURN
    END IF
  END SUBROUTINE READCL

  SUBROUTINE BREAD_YW(FD, YW, M, N, SZ, INFO)
    IMPLICIT NONE
    INTEGER, INTENT(IN) :: FD, M, N
    REAL(KIND=DWP), INTENT(OUT), TARGET :: YW(M,N)
    INTEGER, INTENT(OUT) :: SZ, INFO

    INTEGER :: I, J

    SZ = M * C_SIZEOF(D_ZERO)
    INFO = 0

    !$OMP PARALLEL DEFAULT(NONE) PRIVATE(I,J) SHARED(YW,N,FD,SZ) REDUCTION(MAX:INFO)
    INFO = 0
    !$OMP DO
    DO J = 1, N
       I = BREAD(FD, C_LOC(YW(1,J)), SZ, (J-1) * SZ)
       IF (I .NE. SZ) INFO = MAX(INFO,J)
    END DO
    !$OMP END DO
    !$OMP END PARALLEL

    SZ = SZ * N
  END SUBROUTINE BREAD_YW

  SUBROUTINE BWRITE_SS(FD, SS, N, INFO)
    IMPLICIT NONE
    INTEGER, INTENT(IN) :: FD, N
    REAL(KIND=DWP), INTENT(IN), TARGET :: SS(N)
    INTEGER, INTENT(OUT) :: INFO

    INFO = INT(BWRITE(FD, C_LOC(SS), C_SIZEOF(SS), 0_c_size_t))
  END SUBROUTINE BWRITE_SS

END PROGRAM DGGSVD3T

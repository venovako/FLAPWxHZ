PROGRAM HIF_TEST
  USE HIF_HZ
  USE TIMER
  IMPLICIT NONE

  INTEGER, PARAMETER :: IDIST = 1

  INTEGER :: N, K, P, LDA, NBLTHR, CLK(3), ISEED(4), NRANK, NPLUS, N2PIV, INFO
  DOUBLE PRECISION, ALLOCATABLE :: D(:)
  DOUBLE COMPLEX, ALLOCATABLE :: A(:,:), WORK(:), B(:,:), C(:,:)
  INTEGER, ALLOCATABLE :: IPIV(:), JVEC(:), IPL(:), INVP(:)

  INTEGER :: I,J, II,JJ
  DOUBLE PRECISION :: MXDIF, DTEMP
  CHARACTER(LEN=20) :: ARGV

  DOUBLE PRECISION, EXTERNAL :: DLARND
  EXTERNAL :: DLASRT
  EXTERNAL :: ZLAGHE, ZGEMM

  N = 0
  IF (COMMAND_ARGUMENT_COUNT() .GE. 1) THEN
     CALL GET_COMMAND_ARGUMENT(1, ARGV)
     READ (ARGV,*) N
  ELSE
     WRITE (*,'(A)',ADVANCE='NO') 'N='
     READ (*,*) N
  END IF
  IF (N .LE. 0) STOP 'N <= 0'

  K = 0
  IF (COMMAND_ARGUMENT_COUNT() .GE. 2) THEN
     CALL GET_COMMAND_ARGUMENT(2, ARGV)
     READ (ARGV,*) K
  ELSE
     WRITE (*,'(A)',ADVANCE='NO') 'K='
     READ (*,*) K
  END IF
  IF (K .LT. 0) STOP 'K < 0'
  IF (K .GE. N) STOP 'K >= N'

  P = 0
  IF (COMMAND_ARGUMENT_COUNT() .GE. 3) THEN
     CALL GET_COMMAND_ARGUMENT(3, ARGV)
     READ (ARGV,*) P
  ELSE
     WRITE (*,'(A)',ADVANCE='NO') 'P='
     READ (*,*) P
  END IF
  IF (P .LT. 0) STOP 'P < 0'
  IF (P .GT. N) STOP 'P > N'

  NBLTHR = 0
  IF (COMMAND_ARGUMENT_COUNT() .GE. 4) THEN
     CALL GET_COMMAND_ARGUMENT(4, ARGV)
     READ (ARGV,*) NBLTHR
  ELSE
     WRITE (*,'(A)',ADVANCE='NO') 'NBLTHR='
     READ (*,*) NBLTHR
  END IF
  IF (NBLTHR .LE. 0) STOP 'NBLTHR <= 0'

  IF (BLAS_PREPARE() .LE. 0) STOP 'BLAS_PREPARE'

  LDA = N
  ISEED(1) = 4
  ISEED(2) = 3
  ISEED(3) = 2
  ISEED(4) = 1

  ALLOCATE(D(N))
  D = D_ZERO

  INFO = 1
  DO WHILE (INFO .LE. P)
     D(INFO) = DLARND(IDIST, ISEED)
     IF (D(INFO) .GE. EPSILON(D_ZERO)) INFO = INFO + 1
  END DO
  DO WHILE (INFO .LE. N)
     D(INFO) = -DLARND(IDIST, ISEED)
     IF (D(INFO) .LE. -EPSILON(D_ZERO)) INFO = INFO + 1
  END DO
  CALL DLASRT('I', N, D, INFO)
  IF (INFO .NE. 0) THEN
     WRITE (*,*) INFO
     STOP 'DLASRT'
  END IF

  ALLOCATE(A(LDA,N))
  A = Z_ZERO

  INFO = 2*N
  ALLOCATE(WORK(INFO))
  WORK = Z_ZERO

  INFO = 0
  CALL ZLAGHE(N, K, D, A, LDA, ISEED, WORK, INFO)
  IF (INFO .NE. 0) THEN
     WRITE (*,*) INFO
     STOP 'ZLAGHE'
  END IF

  ALLOCATE(C(LDA,N))
  DO J = 1, N
     DO I = 1, N
#ifndef NDEBUG
        WRITE (*,'(A,ES23.16,A,ES23.16,A)',ADVANCE='NO') '(',DBLE(A(I,J)),',',AIMAG(A(I,J)),')'
#endif
        C(I,J) = -A(I,J)
     END DO
#ifndef NDEBUG
     WRITE (*,*)
#endif
  END DO
#ifndef NDEBUG
  WRITE (*,*)
#endif

  ALLOCATE(IPIV(N))
  ALLOCATE(JVEC(N))
  ALLOCATE(IPL(N))
  ALLOCATE(INVP(N))

  II = BLAS_SET_NUM_THREADS(NBLTHR)
  CALL TIMER_START(CLK)
  CALL HIF_ZHEBPC(N, A, LDA, NRANK, NPLUS, N2PIV, IPIV, JVEC, INFO)
  IF (INFO .GE. 0) THEN
     CALL HIF_JPART(N, NRANK, A, LDA, JVEC, NPLUS, IPL, INVP)
  END IF
  CALL TIMER_STOP(CLK)
  IF (INFO .NE. 0) THEN
     WRITE (*,*) INFO
     STOP 'HIF_ZHEBPC'
  END IF

  ALLOCATE(B(LDA,N))

  DO I = 1, N
     IF (JVEC(I) .GT. 0) THEN
        DO J = 1, N
           B(I,J) = DCONJG(A(J,I))
        END DO
     ELSE IF (JVEC(I) .LT. 0) THEN
        DO J = 1, N
           B(I,J) = -DCONJG(A(J,I))
        END DO
     ELSE
        DO J = 1, N
           B(I,J) = Z_ZERO
        END DO
     END IF
  END DO

  CALL ZGEMM('N', 'N', N, N, N, Z_ONE, A, LDA, B, LDA, Z_ONE, C, LDA)
  II = BLAS_SET_NUM_THREADS(II)

#ifndef NDEBUG
  DO I = 1, N
     DO J = 1, N
        WRITE (*,'(A,ES23.16,A,ES23.16,A)',ADVANCE='NO') '(',DBLE(C(I,J)),',',AIMAG(C(I,J)),')'
     END DO
     WRITE (*,*)
  END DO
  WRITE (*,*)
#endif

  MXDIF = D_ZERO
  II = 1
  JJ = 1
  DO J = 1, N
     DO I = 1, N
        DTEMP = ABS(C(I,J))
        IF (DTEMP .GT. MXDIF) THEN
           II = I
           JJ = J
           MXDIF = DTEMP
        END IF
     END DO
  END DO

  WRITE (*,*) 'NRANK=', NRANK
  WRITE (*,*) 'NPLUS=', NPLUS
  WRITE (*,*) 'N2PIV=', N2PIV
  WRITE (*,*) 'MXDIF=', MXDIF, '@', II, ',', JJ
  WRITE (*,'(A)',ADVANCE='NO') 'TIME='
  CALL TIMER_PRINT(CLK)
  IF (NPLUS .NE. P) STOP 'NPLUS <> P'

  IF (ALLOCATED(B)) DEALLOCATE(B)
  IF (ALLOCATED(INVP)) DEALLOCATE(INVP)
  IF (ALLOCATED(IPL)) DEALLOCATE(IPL)
  IF (ALLOCATED(JVEC)) DEALLOCATE(JVEC)
  IF (ALLOCATED(IPIV)) DEALLOCATE(IPIV)
  IF (ALLOCATED(C)) DEALLOCATE(C)
  IF (ALLOCATED(WORK)) DEALLOCATE(WORK)
  IF (ALLOCATED(A)) DEALLOCATE(A)
  IF (ALLOCATED(D)) DEALLOCATE(D)
END PROGRAM HIF_TEST

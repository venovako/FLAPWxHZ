SUBROUTINE PAR_WORK(FD, BUF, L, G, ATOM, TPC, INFO)
  IMPLICIT NONE

  INTEGER, INTENT(IN) :: FD(6)
  TYPE(Z1MEM), INTENT(INOUT) :: BUF
  INTEGER, INTENT(IN) :: L, G, ATOM
  INTEGER, INTENT(IN) :: TPC
  INTEGER, INTENT(OUT) :: INFO

  INTEGER :: L2, INFO3(3), I, J
  INTEGER :: NRANK, NPLUS, N2PIV
  DOUBLE PRECISION :: DTIMES(3)

  EXTERNAL :: ZGEMM, ZDSCAL

  INFO = 0
  L2 = 2 * L
  INFO3 = 0
  DTIMES = D_MZERO

  IF (.NOT. ASSOCIATED(BUF%X)) ALLOCATE(BUF%X(L2,G),STAT=INFO)
  IF (INFO .GT. 0) STOP 'Error allocating X'
  IF (.NOT. ASSOCIATED(BUF%Y)) ALLOCATE(BUF%Y(L2,G),STAT=INFO)
  IF (INFO .GT. 0) STOP 'Error allocating Y'
  IF (.NOT. ASSOCIATED(BUF%T)) ALLOCATE(BUF%T(L2,L2),STAT=INFO)
  IF (INFO .GT. 0) STOP 'Error allocating T'
  IF (.NOT. ASSOCIATED(BUF%U)) ALLOCATE(BUF%U(L),STAT=INFO)
  IF (INFO .GT. 0) STOP 'Error allocating U'
  IF (.NOT. ASSOCIATED(BUF%IPIV)) ALLOCATE(BUF%IPIV(L2),STAT=INFO)
  IF (INFO .GT. 0) STOP 'Error allocating IPIV'
  IF (.NOT. ASSOCIATED(BUF%JVEC)) ALLOCATE(BUF%JVEC(L2),STAT=INFO)
  IF (INFO .GT. 0) STOP 'Error allocating JVEC'
  IF (.NOT. ASSOCIATED(BUF%IPL)) ALLOCATE(BUF%IPL(L2),STAT=INFO)
  IF (INFO .GT. 0) STOP 'Error allocating IPL'
  IF (.NOT. ASSOCIATED(BUF%INVP)) ALLOCATE(BUF%INVP(L2),STAT=INFO)
  IF (INFO .GT. 0) STOP 'Error allocating INVP'

  J = BLAS_SET_NUM_THREADS(TPC)

  CALL BREAD_XTU(FD, BUF%X, BUF%T, BUF%U, L, G, ATOM, INFO3, INFO)
  IF (INFO .NE. 0) THEN
     WRITE (ULOG,'(A,I11,A,I2)') 'BREAD_XTU(', ATOM, '):', INFO
     INFO = ATOM * 4 + 1
     GOTO 1
  END IF

  CALL TIMER_START(INFO3)
  CALL HIF_ZHEBPC(L2, BUF%T, L2, NRANK, NPLUS, N2PIV, BUF%IPIV, BUF%JVEC, INFO)
  IF (INFO .EQ. 0) CALL HIF_JPART(L2, NRANK, BUF%T, L2, BUF%JVEC, NPLUS, BUF%IPL, BUF%INVP)
  CALL TIMER_STOP(INFO3)
  DTIMES(1) = TIMER2DBLE(INFO3)
  IF (INFO .NE. 0) THEN
     WRITE (ULOG,'(A,I11,A,I11)') 'HIF_ZHEBPC(', ATOM, '):', INFO
     INFO = ATOM * 4 + 2
     GOTO 1
  END IF

  CALL TIMER_START(INFO3)
  CALL ZGEMM('C', 'N', L2, G, L2, Z_ONE, BUF%T, L2, BUF%X, L2, Z_ZERO, BUF%Y, L2)
  CALL TIMER_STOP(INFO3)
  DTIMES(2) = TIMER2DBLE(INFO3)

  CALL TIMER_START(INFO3)
  DO I = 1, L
     CALL ZDSCAL(G, BUF%U(I), BUF%X(L+I,1), L2)
  END DO
  CALL TIMER_STOP(INFO3)
  DTIMES(3) = TIMER2DBLE(INFO3)

  WRITE (UOUT,'(5(I11,A),2(F11.6,A),F11.6)') &
       ATOM,',', NRANK,',', NPLUS,',', N2PIV,',', INFO,',', DTIMES(1),',', DTIMES(2),',', DTIMES(3)

  CALL BWRITE_YWJ(FD(4), BUF%Y, BUF%X, BUF%JVEC, L, G, ATOM, INFO3, INFO)
  IF (INFO .NE. 0) THEN
     WRITE (ULOG,'(A,I11,A,I2)') 'BWRITE_YWJ(', ATOM, '):', INFO
     INFO = ATOM * 4 + 3
     GOTO 1
  END IF

1 J = BLAS_SET_NUM_THREADS(J)
END SUBROUTINE PAR_WORK

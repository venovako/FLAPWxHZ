! Error policy:
! Expected errors that occur at the same place in every thread: INFO + RETURN
! Unexpected errors that should never occur anywhere: STOP + message
! Expected errors that may not occur in every thread: STOP + message
SUBROUTINE PAR_WORK(FD, L, G, ATOM, TPC, INFO)
  IMPLICIT NONE

  INTEGER, INTENT(IN) :: FD(6)
  INTEGER, INTENT(IN) :: L, G, ATOM, TPC
  INTEGER, INTENT(OUT) :: INFO

  DOUBLE COMPLEX, ALLOCATABLE :: X(:,:), T(:,:), Y(:,:)
  DOUBLE PRECISION, ALLOCATABLE :: U(:)
  INTEGER, ALLOCATABLE :: IPIV(:),JVEC(:), IPL(:),INVP(:)
#ifdef USE_X200
  !DIR$ ATTRIBUTES MEMKIND:HBW, ALIGN:ALIGNB :: X,T,Y, U, IPIV,JVEC, IPL,INVP
#else
  !DIR$ ATTRIBUTES ALIGN:ALIGNB :: X,T,Y, U, IPIV,JVEC, IPL,INVP
#endif

  INTEGER :: L2, INFO3(3), I
  INTEGER :: NRANK, NPLUS, N2PIV
  DOUBLE PRECISION :: DTIMES(3)

  EXTERNAL :: ZGEMM, ZDSCAL

  INFO = 0
  L2 = 2 * L
  INFO3 = 0
  DTIMES = D_ZERO

  ALLOCATE(X(L2,G),STAT=INFO)
  IF (INFO .GT. 0) STOP 'Error allocating X'
  ALLOCATE(Y(L2,G),STAT=INFO)
  IF (INFO .GT. 0) STOP 'Error allocating Y'
  ALLOCATE(T(L2,L2),STAT=INFO)
  IF (INFO .GT. 0) STOP 'Error allocating T'
  ALLOCATE(U(L),STAT=INFO)
  IF (INFO .GT. 0) STOP 'Error allocating U'
  ALLOCATE(IPIV(L2),STAT=INFO)
  IF (INFO .GT. 0) STOP 'Error allocating IPIV'
  ALLOCATE(JVEC(L2),STAT=INFO)
  IF (INFO .GT. 0) STOP 'Error allocating JVEC'
  ALLOCATE(IPL(L2),STAT=INFO)
  IF (INFO .GT. 0) STOP 'Error allocating IPL'
  ALLOCATE(INVP(L2),STAT=INFO)
  IF (INFO .GT. 0) STOP 'Error allocating INVP'

  CALL BREAD_XTU(FD, X, T, U, L, G, ATOM, INFO3, INFO)
  IF (INFO .NE. 0) THEN
     WRITE (ULOG,'(A,I11,A,I2)') 'BREAD_XTU(', ATOM, '):', INFO
     STOP
  END IF
#ifndef MKL_NEST_SEQ
  I = BLAS_SET_NUM_THREADS(TPC)
#endif

  CALL TIMER_START(INFO3)
  CALL HIF_ZHEBPC(L2, T, L2, NRANK, NPLUS, N2PIV, IPIV, JVEC, INFO)
  IF (INFO .EQ. 0) CALL HIF_JPART(L2, NRANK, T, L2, JVEC, NPLUS, IPL, INVP)
  CALL TIMER_STOP(INFO3)
  DTIMES(1) = TIMER2DBLE(INFO3)
  IF (INFO .NE. 0) THEN
     WRITE (ULOG,'(A,I11,A,I11)') 'HIF_ZHEBPC(', ATOM, '):', INFO
     STOP
  END IF

  CALL TIMER_START(INFO3)
  CALL ZGEMM('C', 'N', L2, G, L2, Z_ONE, T, L2, X, L2, Z_ZERO, Y, L2)
  CALL TIMER_STOP(INFO3)
  DTIMES(2) = TIMER2DBLE(INFO3)

#ifndef MKL_NEST_SEQ
  I = BLAS_SET_NUM_THREADS(I)
#endif

  CALL TIMER_START(INFO3)
#ifndef MKL_NEST_SEQ
  !$OMP PARALLEL SHARED(U,L,L2,G) PRIVATE(I,INFO) NUM_THREADS(TPC) PROC_BIND(CLOSE)
  INFO = BLAS_SET_NUM_THREADS(1)
  !$OMP DO
#endif
  DO I = 1, L
     CALL ZDSCAL(G, U(I), X(L+I,1), L2)
  END DO
#ifndef MKL_NEST_SEQ
  !$OMP END DO
  INFO = BLAS_SET_NUM_THREADS(INFO)
  !$OMP END PARALLEL
#endif
  CALL TIMER_STOP(INFO3)
  DTIMES(3) = TIMER2DBLE(INFO3)

  WRITE (UOUT,'(5(I11,A),2(F11.6,A),F11.6)') &
       ATOM,',', NRANK,',', NPLUS,',', N2PIV,',', INFO,',', DTIMES(1),',', DTIMES(2),',', DTIMES(3)

  CALL BWRITE_YWJ(FD(4), Y, X, JVEC, L, G, ATOM, INFO3, INFO)
  IF (INFO .NE. 0) THEN
     INFO = INFO + 3
     WRITE (ULOG,'(A,I11,A,I2)') 'BWRITE_YWJ(', ATOM, '):', INFO
     STOP
  END IF

  IF (ALLOCATED(INVP)) DEALLOCATE(INVP)
  IF (ALLOCATED(IPL)) DEALLOCATE(IPL)
  IF (ALLOCATED(JVEC)) DEALLOCATE(JVEC)
  IF (ALLOCATED(IPIV)) DEALLOCATE(IPIV)
  IF (ALLOCATED(U)) DEALLOCATE(U)
  IF (ALLOCATED(T)) DEALLOCATE(T)
  IF (ALLOCATED(Y)) DEALLOCATE(Y)
  IF (ALLOCATED(X)) DEALLOCATE(X)
END SUBROUTINE PAR_WORK

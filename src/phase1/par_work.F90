SUBROUTINE PAR_WORK(FD, BUF, L, G, ATOM, TPC, INFO)
  IMPLICIT NONE

  INTEGER, INTENT(IN) :: FD(6)
  TYPE(Z1MEM), INTENT(INOUT) :: BUF
  INTEGER, INTENT(IN) :: L, G, ATOM
  INTEGER, INTENT(IN) :: TPC
  INTEGER, INTENT(OUT) :: INFO

  INTEGER :: L2, INFO3(3), I, J
  INTEGER :: NRANK, NPLUS, N2PIV
  REAL(KIND=DWP) :: DTIMES(3)
  TYPE(c_ptr) :: PTR

  EXTERNAL :: ZGEMM
#ifndef USE_ZVSCAL
  EXTERNAL :: ZDSCAL
#endif

  INFO = 0
  L2 = 2 * L
  INFO3 = 0
  DTIMES = D_MZERO

  IF (.NOT. ASSOCIATED(BUF%X)) THEN
     CALL BLAS_ALLOC(PTR, ALIGNB, L2 * G * C_SIZEOF(Z_ZERO), INFO)
     IF (INFO .NE. 0) STOP 'Error allocating X'
     CALL C_F_POINTER(PTR, BUF%X, (/ L2, G /))
  END IF
  IF (.NOT. ASSOCIATED(BUF%Y)) THEN
     CALL BLAS_ALLOC(PTR, ALIGNB, L2 * G * C_SIZEOF(Z_ZERO), INFO)
     IF (INFO .NE. 0) STOP 'Error allocating Y'
     CALL C_F_POINTER(PTR, BUF%Y, (/ L2, G /))
  END IF
  IF (.NOT. ASSOCIATED(BUF%T)) THEN
     CALL BLAS_ALLOC(PTR, ALIGNB, L2 * L2 * C_SIZEOF(Z_ZERO), INFO)
     IF (INFO .NE. 0) STOP 'Error allocating T'
     CALL C_F_POINTER(PTR, BUF%T, (/ L2, L2 /))
  END IF
  IF (.NOT. ASSOCIATED(BUF%U)) THEN
     CALL BLAS_ALLOC(PTR, ALIGNB, L * C_SIZEOF(D_ZERO), INFO)
     IF (INFO .NE. 0) STOP 'Error allocating U'
     CALL C_F_POINTER(PTR, BUF%U, (/ L /))
  END IF
  IF (.NOT. ASSOCIATED(BUF%IPIV)) THEN
     CALL BLAS_ALLOC(PTR, ALIGNB, L2 * C_SIZEOF(0), INFO)
     IF (INFO .NE. 0) STOP 'Error allocating IPIV'
     CALL C_F_POINTER(PTR, BUF%IPIV, (/ L2 /))
  END IF
  IF (.NOT. ASSOCIATED(BUF%JVEC)) THEN
     CALL BLAS_ALLOC(PTR, ALIGNB, L2 * C_SIZEOF(0), INFO)
     IF (INFO .NE. 0) STOP 'Error allocating JVEC'
     CALL C_F_POINTER(PTR, BUF%JVEC, (/ L2 /))
  END IF
  IF (.NOT. ASSOCIATED(BUF%IPL)) THEN
     CALL BLAS_ALLOC(PTR, ALIGNB, L2 * C_SIZEOF(0), INFO)
     IF (INFO .NE. 0) STOP 'Error allocating IPL'
     CALL C_F_POINTER(PTR, BUF%IPL, (/ L2 /))
  END IF
  IF (.NOT. ASSOCIATED(BUF%INVP)) THEN
     CALL BLAS_ALLOC(PTR, ALIGNB, L2 * C_SIZEOF(0), INFO)
     IF (INFO .NE. 0) STOP 'Error allocating INVP'
     CALL C_F_POINTER(PTR, BUF%INVP, (/ L2 /))  
  END IF

  J = BLAS_SET_NUM_THREADS(TPC)

  CALL BREAD_XTU(FD, BUF%X, BUF%T, BUF%U, L, G, ATOM, INFO3, INFO)
  IF (INFO .NE. 0) THEN
     WRITE (ULOG,'(A,I11,A,I2)') 'BREAD_XTU(', ATOM, '):', INFO
     INFO = ATOM * 4 + 1
     GOTO 1
  END IF

  INFO3(1) = GET_SYS_US()
  CALL HIF_ZHEBPC(L2, BUF%T, L2, NRANK, NPLUS, N2PIV, BUF%IPIV, BUF%JVEC, INFO)
  IF (INFO .EQ. 0) CALL HIF_JPART(L2, NRANK, BUF%T, L2, BUF%JVEC, NPLUS, BUF%IPL, BUF%INVP)
  INFO3(2) = GET_SYS_US()
  INFO3(1) = INFO3(2) - INFO3(1)
  IF (INFO .NE. 0) THEN
     WRITE (ULOG,'(A,I11,A,I11)') 'HIF_ZHEBPC(', ATOM, '):', INFO
     INFO = ATOM * 4 + 2
     GOTO 1
  END IF

  CALL ZGEMM('C', 'N', L2, G, L2, Z_ONE, BUF%T, L2, BUF%X, L2, Z_ZERO, BUF%Y, L2)
  INFO3(3) = GET_SYS_US()
  INFO3(2) = INFO3(3) - INFO3(2)

#ifdef USE_ZVSCAL
  DO I = 1, G
     CALL BLAS_ZVSCAL(L, BUF%U, BUF%X(L+1:L2,I))
  END DO
#else
  DO I = 1, L
     CALL ZDSCAL(G, BUF%U(I), BUF%X(L+I,1), L2)
  END DO
#endif
  INFO3(3) = GET_SYS_US() - INFO3(3)

  !DIR$ VECTOR ALWAYS
  DO I = 1, 3
     DTIMES(I) = INFO3(I) * DUS2S
  END DO

  WRITE (UOUT,'(5(I11,A),2(F11.6,A),F11.6)') &
       ATOM,',', NRANK,',', NPLUS,',', N2PIV,',', INFO,',', DTIMES(1),',', DTIMES(2),',', DTIMES(3)

  CALL BWRITE_YWJ(FD(4), BUF%Y, BUF%X, BUF%JVEC, L, G, ATOM, INFO3, INFO)
  IF (INFO .NE. 0) THEN
     WRITE (ULOG,'(A,I11,A,I2)') 'BWRITE_YWJ(', ATOM, '):', INFO
     INFO = ATOM * 4 + 3
     GOTO 1
  END IF

1 J = BLAS_SET_NUM_THREADS(J)
END SUBROUTINE PAR_WORK

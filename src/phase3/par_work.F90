! Error policy:
! Expected errors that occur at the same place in every thread: INFO + RETURN
! Unexpected errors that should never occur anywhere: STOP + message
! Expected errors that may not occur in every thread: STOP + message
! (Otherwise it would be impractical to keep jumping from one barrier to another
! in a thread that has failed, doing nothing, just too keep everything in sync.)
SUBROUTINE PAR_WORK(FD, M,N, CPR,TPC, JSTRAT,NSWP, PSHBUF,PSTATS, MXTIME,INFO)
  IMPLICIT NONE

  INTEGER, INTENT(IN) :: FD(12), M,N, CPR,TPC, JSTRAT(2),NSWP(2)
  TYPE(ZSHENT), INTENT(OUT) :: PSHBUF(CPR)
  DOUBLE PRECISION, INTENT(OUT) :: MXTIME
  INTEGER, INTENT(OUT) :: PSTATS(8), INFO

  DOUBLE COMPLEX, ALLOCATABLE, TARGET :: Y(:,:),W(:,:), YU(:,:),WV(:,:), Z(:,:),ZZ(:,:)
  DOUBLE COMPLEX, ALLOCATABLE :: BH(:,:), BS(:,:), BZ(:,:)
  DOUBLE PRECISION, ALLOCATABLE, TARGET :: E(:), SS(:), EY(:),SY(:), EW(:),SW(:)
  INTEGER, ALLOCATABLE :: NCOLSB(:),IFCOLB(:), JSARR(:,:),JSBARR(:,:,:), JSPAIR(:,:,:),JSCOMM(:,:,:)
  INTEGER, ALLOCATABLE, TARGET :: JS(:,:), JSPIN1(:,:,:),JSPIN2(:,:,:), J(:), IWORK(:)
  !DIR$ ATTRIBUTES ALIGN:ALIGNB :: Y,W, YU,WV, Z,ZZ
#ifdef USE_X200
  !DIR$ ATTRIBUTES MEMKIND:HBW, ALIGN:ALIGNB :: BH,BS,BZ
  !DIR$ ATTRIBUTES MEMKIND:HBW, ALIGN:ALIGNB :: E, SS, EY,SY, EW,SW
  !DIR$ ATTRIBUTES MEMKIND:HBW, ALIGN:ALIGNB :: NCOLSB,IFCOLB, JSARR,JSBARR, JSPAIR,JSCOMM
  !DIR$ ATTRIBUTES MEMKIND:HBW, ALIGN:ALIGNB :: JS,JSPIN1,JSPIN2, J, IWORK
#else
  !DIR$ ATTRIBUTES ALIGN:ALIGNB :: BH,BS,BZ
  !DIR$ ATTRIBUTES ALIGN:ALIGNB :: E, SS, EY,SY, EW,SW
  !DIR$ ATTRIBUTES ALIGN:ALIGNB :: NCOLSB,IFCOLB, JSARR,JSBARR, JSPAIR,JSCOMM
  !DIR$ ATTRIBUTES ALIGN:ALIGNB :: JS,JSPIN1,JSPIN2, J, IWORK
#endif

  INTEGER :: IAM
  INTEGER :: NPAIRS(2),NSTEPS(2), NBCOLS, NBSTEPS,NBPAIRS
  INTEGER :: MINNCOLSB,MAXNCOLSB, MINCB2,MXNCB2,LDB
  INTEGER :: I, K
  INTEGER :: IFCOL,NCOLS
  INTEGER :: SZ(12), CLK

#ifdef NDEBUG
  !DIR$ ASSUME (M .GT. 0)
  !DIR$ ASSUME (N .GT. 0)
  !DIR$ ASSUME (CPR .GE. 1)
#ifndef MKL_NEST_SEQ
  !DIR$ ASSUME (TPC .GE. 1)
#else
  !DIR$ ASSUME (TPC .EQ. 0)
#endif
#else
  IF (M .LE. 0) STOP 'PAR_WORK: M .LE. 0'
  IF (N .LE. 0) STOP 'PAR_WORK: N .LE. 0'
  IF (CPR .LT. 1) STOP 'PAR_WORK: CPR .LT. 1'
#ifndef MKL_NEST_SEQ
  IF (TPC .LT. 1) STOP 'PAR_WORK: TPC .LT. 1'
#else
  IF (TPC .NE. 0) STOP 'PAR_WORK: TPC .NE. 0'
#endif
#endif

  INFO = 0
  MXTIME = D_ZERO

  IAM = OMP_GET_THREAD_NUM() + 1

  NBCOLS = 2 * CPR
  NBPAIRS = NBCOLS / 2

  ! N >= NBCOLS?
  MINNCOLSB = N / NBCOLS
  IF (MINNCOLSB .LE. 0) THEN
     INFO = 1
     RETURN
  END IF
  MINCB2 = 2 * MINNCOLSB
  NPAIRS(1) = MINCB2 / 2

  ALLOCATE(JS(8,3),STAT=INFO)
  IF (INFO .GT. 0) STOP 'Error allocating JS'

  CALL JSTRAT_INIT(JS(1,1), JSTRAT(1), MINCB2, NSTEPS(1))
  IF (NSTEPS(1) .LE. 0) STOP 'Error in JSTRAT_INIT(1)'
  JS(7,1) = NSTEPS(1)
  JS(8,1) = NPAIRS(1)

  MAXNCOLSB = MINNCOLSB
  K = MOD(N, NBCOLS)
  IF (K .GT. 0) MAXNCOLSB = MAXNCOLSB + 1
  MXNCB2 = 2 * MAXNCOLSB
  LDB = LDALIGN(MXNCB2, ZALIGN)
  NPAIRS(2) = MXNCB2 / 2

  CALL JSTRAT_INIT(JS(1,2), JSTRAT(1), MXNCB2, NSTEPS(2))
  IF (NSTEPS(2) .LE. 0) STOP 'Error in JSTRAT_INIT(2)'
  JS(7,2) = NSTEPS(2)
  JS(8,2) = NPAIRS(2)

  CALL JSTRAT_INIT(JS(1,3), JSTRAT(2), NBCOLS, NBSTEPS)
  IF (NBSTEPS .LE. 0) STOP 'Error in JSTRAT_INIT(3)'
  JS(7,3) = NBSTEPS
  JS(8,3) = NBPAIRS

  ALLOCATE(NCOLSB(NBCOLS),STAT=INFO)
  IF (INFO .GT. 0) STOP 'Error allocating NCOLSB'
  ALLOCATE(IFCOLB(NBCOLS),STAT=INFO)
  IF (INFO .GT. 0) STOP 'Error allocating IFCOLB'

  DO I = 1, K
     NCOLSB(I) = MAXNCOLSB
  END DO
  DO I = K+1, NBCOLS
     NCOLSB(I) = MINNCOLSB
  END DO

  IFCOLB(1) = 1
  DO I = 2, NBCOLS
     IFCOLB(I) = IFCOLB(I-1) + NCOLSB(I-1)
  END DO

  ALLOCATE(JSARR(2,NPAIRS(2)),STAT=INFO)
  IF (INFO .GT. 0) STOP 'Error allocating JSARR'

  ALLOCATE(JSPIN1(2,NPAIRS(1),NSTEPS(1)),STAT=INFO)
  IF (INFO .GT. 0) STOP 'Error allocating JSPIN1'

  DO I = 1, NSTEPS(1)
     CALL JSTRAT_NEXT_NC(JS(1,1), JSARR, INFO)
     CALL JSTRAT_UNPACK_NC(JS(1,1), JSARR, JSPIN1(1,1,I), INFO)
     IF (INFO .NE. NPAIRS(1)) STOP 'Error in JSTRAT_NEXT/UNPACK_NC(1)'
  END DO

  ALLOCATE(JSPIN2(2,NPAIRS(2),NSTEPS(2)),STAT=INFO)
  IF (INFO .GT. 0) STOP 'Error allocating JSPIN2'

  DO I = 1, NSTEPS(2)
     CALL JSTRAT_NEXT_NC(JS(1,2), JSARR, INFO)
     CALL JSTRAT_UNPACK_NC(JS(1,2), JSARR, JSPIN2(1,1,I), INFO)
     IF (INFO .NE. NPAIRS(2)) STOP 'Error in JSTRAT_NEXT/UNPACK_NC(2)'
  END DO

  ALLOCATE(JSBARR(2,2,NBPAIRS),STAT=INFO)
  IF (INFO .GT. 0) STOP 'Error allocating JSBARR'
  ALLOCATE(JSPAIR(2,NBPAIRS,NBSTEPS),STAT=INFO)
  IF (INFO .GT. 0) STOP 'Error allocating JSPAIR'
  ALLOCATE(JSCOMM(2,NBPAIRS,NBSTEPS),STAT=INFO)
  IF (INFO .GT. 0) STOP 'Error allocating JSCOMM'

  DO I = 1, NBSTEPS
     CALL JSTRAT_NEXT_WC(JS(1,3), JSBARR, INFO)
     CALL JSTRAT_UNPACK_WC(JS(1,3), JSBARR, JSPAIR(1,1,I), JSCOMM(1,1,I), INFO)
     IF (INFO .NE. NBPAIRS) STOP 'Error in JSTRAT_NEXT/UNPACK_WC'
  END DO

  ALLOCATE(Y(M,MXNCB2),STAT=INFO)
  IF (INFO .GT. 0) STOP 'Error allocating Y'
  ALLOCATE(W(M,MXNCB2),STAT=INFO)
  IF (INFO .GT. 0) STOP 'Error allocating W'
  ALLOCATE(YU(M,MXNCB2),STAT=INFO)
  IF (INFO .GT. 0) STOP 'Error allocating YU'
  ALLOCATE(WV(M,MXNCB2),STAT=INFO)
  IF (INFO .GT. 0) STOP 'Error allocating WV'
  ALLOCATE(Z(N,MXNCB2),STAT=INFO)
  IF (INFO .GT. 0) STOP 'Error allocating Z'
  ALLOCATE(ZZ(N,MXNCB2),STAT=INFO)
  IF (INFO .GT. 0) STOP 'Error allocating ZZ'
  ALLOCATE(BH(LDB,MXNCB2),STAT=INFO)
  IF (INFO .GT. 0) STOP 'Error allocating BH'
  ALLOCATE(BS(LDB,MXNCB2),STAT=INFO)
  IF (INFO .GT. 0) STOP 'Error allocating BS'
  ALLOCATE(BZ(LDB,MXNCB2),STAT=INFO)
  IF (INFO .GT. 0) STOP 'Error allocating BZ'
  ALLOCATE(E(MXNCB2),STAT=INFO)
  IF (INFO .GT. 0) STOP 'Error allocating E'
  ALLOCATE(SS(MXNCB2),STAT=INFO)
  IF (INFO .GT. 0) STOP 'Error allocating SS'
  ALLOCATE(SY(MXNCB2),STAT=INFO)
  IF (INFO .GT. 0) STOP 'Error allocating SY'
  ALLOCATE(EY(MXNCB2),STAT=INFO)
  IF (INFO .GT. 0) STOP 'Error allocating EY'
  ALLOCATE(EW(MXNCB2),STAT=INFO)
  IF (INFO .GT. 0) STOP 'Error allocating EW'
  ALLOCATE(SW(MXNCB2),STAT=INFO)
  IF (INFO .GT. 0) STOP 'Error allocating SW'
  ALLOCATE(J(M),STAT=INFO)
  IF (INFO .GT. 0) STOP 'Error allocating J'
  K = 2*((M/2)+MOD(M,2))+6*MXNCB2
  ALLOCATE(IWORK(K),STAT=INFO)
  IF (INFO .GT. 0) STOP 'Error allocating IWORK'

  ! READ 1st BCOL
  I = JSPAIR(1,IAM,1)
  NCOLS = NCOLSB(I)
  IFCOL = IFCOLB(I)
  K = 1 + (MAXNCOLSB - NCOLS)
  CALL BREAD_YW(FD, Y(1,K), W(1,K), M, IFCOL, NCOLS, SZ, INFO)
  IF (INFO .NE. 0) STOP 'Error in BREAD_YW(1)'

  ! READ 2nd BCOL
  I = JSPAIR(2,IAM,1)
  NCOLS = NCOLSB(I)
  IFCOL = IFCOLB(I)
  K = 1 + MAXNCOLSB
  CALL BREAD_YW(FD, Y(1,K), W(1,K), M, IFCOL, NCOLS, SZ, INFO)
  IF (INFO .NE. 0) STOP 'Error in BREAD_YW(2)'

  CALL BREAD_J(FD(3), J, M, SZ(3), INFO)
  IF (INFO .NE. 0) STOP 'Error in BREAD_J'

  PSHBUF(IAM)%Y => Y
  PSHBUF(IAM)%W => W
  PSHBUF(IAM)%Z => Z
  !$OMP BARRIER

  K = MXNCB2
  CLK = GET_THREAD_NS()
#ifndef MKL_NEST_SEQ
  CALL ZHZL2(M,N,K, Y,YU,M, W,WV,M, J, Z,ZZ,N, IAM,CPR,TPC, JS,NSWP,&
       NCOLSB,IFCOLB, JSPIN1,JSPIN2,JSPAIR,JSCOMM, PSHBUF,PSTATS, E,SS, EY,SY, EW,SW,&
       BH,BS,BZ,LDB, IWORK, INFO)
#else
  CALL ZHZL2(M,N,K, Y,YU,M, W,WV,M, J, Z,ZZ,N, IAM,CPR, JS,NSWP,&
       NCOLSB,IFCOLB, JSPIN1,JSPIN2,JSPAIR,JSCOMM, PSHBUF,PSTATS, E,SS, EY,SY, EW,SW,&
       BH,BS,BZ,LDB, IWORK, INFO)
#endif
  CLK = GET_THREAD_NS() - CLK

  MXTIME = CLK * DNS2S
  !$OMP BARRIER

  ! WRITE 1st BCOL
  I = JSPAIR(1,IAM,1)
  NCOLS = NCOLSB(I)
  IFCOL = IFCOLB(I)
  K = 1 + (MAXNCOLSB - NCOLS)
  CALL BWRITE_EZS(FD(4), E(K), Z(1,K), SS(K), Y(1,K), EY(K), SY(K), W(1,K), EW(K), SW(K), M, N, IFCOL, NCOLS, I)
  IF (I .NE. 0) INFO = I + 1
  ! WRITE 2nd BCOL
  I = JSPAIR(2,IAM,1)
  NCOLS = NCOLSB(I)
  IFCOL = IFCOLB(I)
  K = 1 + MAXNCOLSB
  CALL BWRITE_EZS(FD(4), E(K), Z(1,K), SS(K), Y(1,K), EY(K), SY(K), W(1,K), EW(K), SW(K), M, N, IFCOL, NCOLS, I)
  IF (I .NE. 0) INFO = I + 1

  IF (ALLOCATED(IWORK)) DEALLOCATE(IWORK)
  IF (ALLOCATED(J)) DEALLOCATE(J)
  IF (ALLOCATED(SW)) DEALLOCATE(SW)
  IF (ALLOCATED(EW)) DEALLOCATE(EW)
  IF (ALLOCATED(EY)) DEALLOCATE(EY)
  IF (ALLOCATED(SY)) DEALLOCATE(SY)
  IF (ALLOCATED(SS)) DEALLOCATE(SS)
  IF (ALLOCATED(E)) DEALLOCATE(E)
  IF (ALLOCATED(BZ)) DEALLOCATE(BZ)
  IF (ALLOCATED(BS)) DEALLOCATE(BS)
  IF (ALLOCATED(BH)) DEALLOCATE(BH)
  IF (ALLOCATED(ZZ)) DEALLOCATE(ZZ)
  IF (ALLOCATED(Z)) DEALLOCATE(Z)
  PSHBUF(IAM)%Z => NULL()
  IF (ALLOCATED(WV)) DEALLOCATE(WV)
  IF (ALLOCATED(YU)) DEALLOCATE(YU)
  IF (ALLOCATED(W)) DEALLOCATE(W)
  PSHBUF(IAM)%W => NULL()
  IF (ALLOCATED(Y)) DEALLOCATE(Y)
  PSHBUF(IAM)%Y => NULL()
  IF (ALLOCATED(JSCOMM)) DEALLOCATE(JSCOMM)
  IF (ALLOCATED(JSPAIR)) DEALLOCATE(JSPAIR)
  IF (ALLOCATED(JSBARR)) DEALLOCATE(JSBARR)
  IF (ALLOCATED(JSPIN2)) DEALLOCATE(JSPIN2)
  IF (ALLOCATED(JSPIN1)) DEALLOCATE(JSPIN1)
  IF (ALLOCATED(JSARR)) DEALLOCATE(JSARR)
  IF (ALLOCATED(IFCOLB)) DEALLOCATE(IFCOLB)
  IF (ALLOCATED(NCOLSB)) DEALLOCATE(NCOLSB)
  IF (ALLOCATED(JS)) DEALLOCATE(JS)
END SUBROUTINE PAR_WORK

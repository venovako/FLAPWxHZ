SUBROUTINE ZUPXFER(IAM,BSTEP, MXNC,NCP,NCQ,NCB, M,N,K, Y,YU,LDY, W,WV,LDW, Z,ZZ,LDZ, BZ,LDB,&
     JSCOMM,CPR,NBSTEPS, PSHBUF, LNOROT)
  IMPLICIT NONE

  INTEGER, INTENT(IN) :: IAM,BSTEP, MXNC,NCP,NCQ,NCB, M,N,K, LDY,LDW,LDZ,LDB,&
       CPR,NBSTEPS,JSCOMM(2,CPR,NBSTEPS)
  LOGICAL, INTENT(IN) :: LNOROT
  TYPE(ZSHENT), INTENT(IN) :: PSHBUF(CPR)
  COMPLEX(KIND=DWP), INTENT(IN) :: Y(LDY,K),W(LDW,K),Z(LDZ,K), BZ(LDB,NCB)
  COMPLEX(KIND=DWP), INTENT(OUT), TARGET :: YU(LDY,K),WV(LDW,K),ZZ(LDZ,K)

  INTEGER :: I, TGT

  COMPLEX(KIND=DWP), POINTER, CONTIGUOUS :: SRC_Y1(:,:),SRC_W1(:,:),SRC_Z1(:,:)
  COMPLEX(KIND=DWP), POINTER, CONTIGUOUS :: TGT_Y1(:,:),TGT_W1(:,:),TGT_Z1(:,:)
  COMPLEX(KIND=DWP), POINTER, CONTIGUOUS :: SRC_Y2(:,:),SRC_W2(:,:),SRC_Z2(:,:)
  COMPLEX(KIND=DWP), POINTER, CONTIGUOUS :: TGT_Y2(:,:),TGT_W2(:,:),TGT_Z2(:,:)

  ! Transfer Q.
  TGT = JSCOMM(2,IAM,BSTEP)
  SRC_Y2 => YU(:,MXNC+1:MXNC+NCQ) ! 1:LDY
  SRC_W2 => WV(:,MXNC+1:MXNC+NCQ) ! 1:LDW
  SRC_Z2 => ZZ(:,MXNC+1:MXNC+NCQ) ! 1:LDZ

  IF (TGT .LT. 0) THEN ! going left
     I = 1 + (MXNC - NCQ)
     TGT_Y2 => PSHBUF(-TGT)%Y(:,I:I+NCQ-1) ! 1:LDY
     TGT_W2 => PSHBUF(-TGT)%W(:,I:I+NCQ-1) ! 1:LDW
     TGT_Z2 => PSHBUF(-TGT)%Z(:,I:I+NCQ-1) ! 1:LDZ
  ELSE IF (TGT .GT. 0) THEN ! going right
     TGT_Y2 => PSHBUF(TGT)%Y(:,MXNC+1:MXNC+NCQ) ! 1:LDY
     TGT_W2 => PSHBUF(TGT)%W(:,MXNC+1:MXNC+NCQ) ! 1:LDW
     TGT_Z2 => PSHBUF(TGT)%Z(:,MXNC+1:MXNC+NCQ) ! 1:LDZ
  ELSE ! should never happen
     STOP 'TGT_Q .EQ. 0'
  END IF

  ! Transfer P.
  TGT = JSCOMM(1,IAM,BSTEP)
  I = 1 + (MXNC - NCP)
  SRC_Y1 => YU(:,I:I+NCP-1) ! 1:LDY
  SRC_W1 => WV(:,I:I+NCP-1) ! 1:LDW
  SRC_Z1 => ZZ(:,I:I+NCP-1) ! 1:LDZ

  IF (TGT .LT. 0) THEN ! going left
     TGT_Y1 => PSHBUF(-TGT)%Y(:,I:I+NCP-1) ! 1:LDY
     TGT_W1 => PSHBUF(-TGT)%W(:,I:I+NCP-1) ! 1:LDW
     TGT_Z1 => PSHBUF(-TGT)%Z(:,I:I+NCP-1) ! 1:LDZ
  ELSE IF (TGT .GT. 0) THEN ! going right
     TGT_Y1 => PSHBUF(TGT)%Y(:,MXNC+1:MXNC+NCP) ! 1:LDY
     TGT_W1 => PSHBUF(TGT)%W(:,MXNC+1:MXNC+NCP) ! 1:LDW
     TGT_Z1 => PSHBUF(TGT)%Z(:,MXNC+1:MXNC+NCP) ! 1:LDZ
  ELSE ! should never happen
     STOP 'ZHZL2: TGT_P .EQ. 0'
  END IF

  CALL ZUPDATE(M,NCB, Y(1,I),YU(1,I),LDY, BZ,LDB, LNOROT)
  CALL ZUPDATE(M,NCB, W(1,I),WV(1,I),LDW, BZ,LDB, LNOROT)
  CALL ZUPDATE(N,NCB, Z(1,I),ZZ(1,I),LDZ, BZ,LDB, LNOROT)
  !$OMP BARRIER
  CALL ZXFER2(M,NCP,NCQ, SRC_Y1,SRC_Y2, LDY, TGT_Y1,TGT_Y2, LDY)
  CALL ZXFER2(M,NCP,NCQ, SRC_W1,SRC_W2, LDW, TGT_W1,TGT_W2, LDW)
  CALL ZXFER2(N,NCP,NCQ, SRC_Z1,SRC_Z2, LDZ, TGT_Z1,TGT_Z2, LDZ)
END SUBROUTINE ZUPXFER
